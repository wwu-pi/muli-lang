plugins {
  id 'java'
  id 'org.jastadd' version '1.12.0'
}

group 'de.wwu.muli'
version '1.5-SNAPSHOT'

defaultTasks 'jar'

if (!file('extendj/jastadd_modules').exists()) {
  throw new GradleException('ExtendJ seems to be missing. Please run "git submodule init", then "git submodule update".')
}

jastadd {
	configureModuleBuild()

//	modules 'jastadd_modules' // Load module specifications from this file.

	modules {
		include("extendj/jastadd_modules") // Includes the core ExtendJ modules.

		module "muli-lang frontend", {
			moduleName "Muli Lang (based on Java SE 8)"
			moduleVariant "frontend"

			imports "java8 frontend" // This module depends on "java8 frontend" from ExtendJ.

			java {
				basedir "src/java/"
				include "**/*.java"
			}

			jastadd {
				basedir "src/frontend/"
				include "**/*.ast"
				include "**/*.jadd"
				include "**/*.jrag"
			}

			scanner {
				basedir "src/scanner/"
				include "**/*.flex"
			}

			parser {
				basedir "src/parser/"
				include "**/*.parser"
			}
		}

		module("muli-lang backend") {
			moduleName "Muli Lang Backend (based on Java SE 8)"
			moduleVariant "backend"

			imports "muli-lang frontend"
			imports "java8 backend"

			jastadd {
				basedir "src/backend"
				include "**/*.jadd"
				include "**/*.jrag"
			}
		}
	}

	// Target module to build:
	module = 'muli-lang backend'

	astPackage = 'org.extendj.ast'
	genDir = 'src/gen/java'
	buildInfoDir = 'src/gen-res'
	parser.name = 'JavaParser'
	parser.genDir = 'src/gen/java/org/extendj/parser'
	scanner.name = 'OriginalScanner'
	scanner.genDir = 'src/gen/java/org/extendj/scanner'
}

sourceSets.main {
	java {
		srcDir 'extendj/src/frontend-main'
		srcDir 'extendj/src/backend-main'
//		exclude 'org/extendj/PrettyPrintTask.java' // Avoid dependency on Apache Ant.
//		exclude 'org/extendj/JastAddJTask.java' // Avoid dependency on Apache Ant.
	}
	resources {
		srcDir 'extendj/src/res'
		srcDir jastadd.buildInfoDir
	}
}

//jar.manifest.attributes 'Main-Class': 'de.wwu.muli.FrontendMain'
jar.manifest.attributes 'Main-Class': 'de.wwu.muli.BackendMain'
jar.destinationDir = projectDir

// Java -source and -target version.
sourceCompatibility = targetCompatibility = '1.8'

task sourceZip(type: Zip) {
	description 'Builds a Zip file with the entire repository (including the ExtendJ submodule).'
	destinationDir = projectDir
	archiveName = "muli-lang.zip"

	from (projectDir) {
		exclude '**/.git'
		exclude '**/.gitignore'
		exclude '**/.gitattributes'
		exclude '**/.gitmodules'
		exclude '**/.idea'
		exclude 'build'
		exclude 'bin'
		exclude '.gradle'
		exclude '.classpath'
		exclude '.settings'
		exclude '.project'
		exclude 'src/gen'
		exclude 'src/gen-res'
		exclude '*.jar'
		exclude '*.zip'
		exclude '**/*.swp'
	}

	into 'muli-lang'
}

repositories{
	mavenLocal()
}

configurations {
	mulicp
}

dependencies {
	mulicp 'de.wwu.muli:muli-classpath:1.5.0+'
}

jar {
	into("/" ) {
		from(configurations.mulicp) {
			rename { "mulicp.jar" }
		}
	}
}
