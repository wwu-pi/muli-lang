buildscript {
	repositories.mavenCentral()
	repositories.mavenLocal()
	dependencies {
		classpath 'org.jastadd:jastaddgradle:1.10.0'
	}
}

apply plugin: 'java'
apply plugin: 'jastadd'

defaultTasks 'jar'

jastadd {
	configureModuleBuild()

	modules 'jastadd_modules' // Load module specifications from this file.

	// Target module to build:
	module = 'muli-lang backend'

	astPackage = 'org.extendj.ast'
	genDir = 'src/gen/java'
	buildInfoDir = 'src/gen-res'
	parser.name = 'JavaParser'
}

sourceSets.main {
	java {
		srcDir 'extendj/src/frontend-main'
		srcDir 'extendj/src/backend-main'
		exclude 'org/extendj/PrettyPrintTask.java' // Avoid dependency on Apache Ant.
		exclude 'org/extendj/JastAddJTask.java' // Avoid dependency on Apache Ant.
	}
	resources {
		srcDir 'extendj/src/res'
		srcDir jastadd.buildInfoDir
	}
}

//jar.manifest.attributes 'Main-Class': 'org.extendj.FrontendMain'
jar.manifest.attributes 'Main-Class': 'org.extendj.BackendMain'
jar.destinationDir = projectDir

// Java -source and -target version.
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

task sourceZip(type: Zip) {
	description 'Builds a Zip file with the entire repository (including the ExtendJ submodule).'
	destinationDir = projectDir
	archiveName = "muli-lang.zip"

	from (projectDir) {
		exclude '**/.git'
		exclude '**/.gitignore'
		exclude '**/.gitattributes'
		exclude '**/.gitmodules'
		exclude '**/.idea'
		exclude 'build'
		exclude 'bin'
		exclude '.gradle'
		exclude '.classpath'
		exclude '.settings'
		exclude '.project'
		exclude 'src/gen'
		exclude 'src/gen-res'
		exclude '*.jar'
		exclude '*.zip'
		exclude '**/*.swp'
	}

	into 'muli-lang'
}
